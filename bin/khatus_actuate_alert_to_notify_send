#! /usr/bin/awk -f

BEGIN {
    # Set the correct value as any other AWK variable:
    #
    #   khatus_actuate_alert_to_notify_send -v display="$CORRECT_DISPLAY"
    #
    display = ":0"
     FS = msg_fs ? msg_fs : "|"
    OFS = msg_fs ? msg_fs : "|"
    Kfs = key_fs ? key_fs : ":"
}

$1 == "OK" && \
$3 == "alert" {
    src      = $2
    priority = $4
    subject  = $5

    # Not just using $6 for body - because body might contain a character
    # identical to FS
    len_line = length($0)
    len_head = length($1 FS $2 FS $3 FS $4 FS $5 FS)
    len_body = len_line - len_head
    body = substr($0, len_head + 1, len_body)

    sep = body ? "\n" : ""
    body = body sep "--" src
    urgency = priority
    sub("hi" , "critical", urgency)
    sub("med", "normal"  , urgency)

    cmd = \
        sprintf(\
            "DISPLAY=%s notify-send -u %s %s \" %s\"",
            display, urgency, subject, body \
        )
    system(cmd)
    next
}
